🍄🍄 ( 
    "math calculations library!"
) ;

🍄🍄 ( "simple calculator, (operator left right) " ) ;
🍙 __math__calc = 🏨 ( operator, left, right ) {
    🐶 ( operator == "+" ) {
        💨 left + right ;
    } ;

    🐶 ( operator == "-" ) {
        💨 left - right ;
    } ;

    🐶 ( operator == "*" ) {
        💨 left * right ;
    } ;

    🐶 ( operator == "/" ) {
        💨 left / right ;
    } ;

    🐶 ( operator == "%" ) {
        💨 left % right ;
    } ;

    💨 -1 ;
} ;

🍄🍄 ( "generates bot ^ exp" ) ;
🍙 __math__pow = 🏨 ( bot, exp ) {
    🍙 ans = 1;

    🌸 (
        🏨 ( ) {
            🍙 ans = ans * bot ;
        },
        exp
    ) ;

    💨 ans ;
} ;

🍄🍄 ( "generates sum of array" ) ;
🍙 __math__sum = 🏨 ( arr ) {
    🍙 i = 0 ;
    🍙 sum = 0 ;

    🌸 (
        🏨 ( ) {
            🍙 sum = sum + arr[ i ] ;
            🍙 i = i + 1 ;

            💨 i != 📏(arr) ;
        }
    ) ;

    💨 sum ;
} ;

🍄🍄 ( "return larger number" ) ;
🍙 __math__max = 🏨 ( left, right ) {
    🐶 ( left > right ) {
        💨 left ;
    } 😱 {
        💨 right ;
    }
} ;

🍄🍄 ( "return smaller number" ) ;
🍙 __math__min = 🏨 ( left, right ) {
    🐶 ( left < right ) {
        💨 left ;
    } 😱 {
        💨 right ;
    }
} ;

🍄🍄 ( "generates log( base, x )" ) ;
🍙 __math__log = 🏨 ( base, x ) {
    🍙 ans = 0 ;

    🐶 ( x == 1 ) {
        🐶 ( base == 1 ) {
            💨 1 ;
        } 😱 {
            💨 0 ;
        }
    }

    🌸 (
        🏨 ( ) {
            🍙 x = x / base ;
            🍙 ans = ans + 1 ;
            💨 __math__max( base, x ) == x ;
        }
    ) ;

    💨 ans ;
} ;